import TransformationUtils;
import de.cooperateproject.qvtoutils.CooperateLibrary;

transformation UMLtoEA(in uml: UML, in notation : NOTATION, in ecorePrimitives : UML, inout om : OM);
modeltype UML uses uml('http://www.eclipse.org/uml2/5.0.0/UML');
modeltype NOTATION uses notation('http://www.eclipse.org/gmf/runtime/1.0.2/notation');
modeltype OM uses eaobjectmodel('de.cooperateproject.eabridge.eaobjectmodel');

main() {
	var rootModels := uml.rootObjects()[Model];
	assert fatal (rootModels->size() = 1);
    
	log("Found UML Root Models", rootModels);
	
	var eaPckg = om.rootObjects()[OM::Package]->any(true);
	
	log("Found EAOM Root Model", eaPckg);
	
	eaPckg.map mapRoot();
}

mapping inout eaobjectmodel::Package::mapRoot() {
    log("Mapping model root", self);
	self.Packages := uml.rootObjects()[Model]->map mapPackage();
	self.Packages->any(true).Diagrams := notation.objectsOfType(Diagram)->map mapDiagram();
	self.Elements := uml.rootObjects()[Model]->map mapCommentableToElement();
} 

mapping uml::Element::mapCommentableToElement() : eaobjectmodel::Element
disjuncts uml::Package::mapPackageToElement, uml::Classifier::mapClassifierToElement;

mapping uml::Package::mapPackage() : eaobjectmodel::Package {
	log("Mapping package", self);
	PackageGUID := if PackageGUID != null then PackageGUID else generateUUID() endif;
	
	Name := self.name;
	
	var umlPackages := self.getFeature("nestedPackage")->oclAsType(uml::Package);
	Packages := umlPackages->map mapPackage();
	
	// Create package element
	Elements := umlPackages->map mapCommentableToElement();
	
	// Map Package content
	var umlPackagedElements := self.getFeature("packagedElement")->oclAsType(uml::PackageableElement);
	
	
	Elements += umlPackagedElements->selectByKind(uml::Classifier)->map mapCommentableToElement();
				// the connectors are assigned via the eOpposite relation inside the association
				umlPackagedElements->selectByType(uml::Association)->map mapAssociationToConnector();
	Elements += self.getFeature("ownedComment")->oclAsType(uml::Comment)->map mapCommentToElement();
	if (self.oclIsKindOf(uml::Model)) {
	   if (Flags.oclIsUndefined()) then Flags := "" endif;
	   if (Flags.indexOf("isModel=1") <= 0) {
		  Flags := if (Flags.length() > 0) then ";" else "" endif + "isModel=1";
		} endif;
	};
	
}

mapping uml::Classifier::mapClassifierToElement() : eaobjectmodel::Element
disjuncts uml::Class::mapClassToElement, uml::Interface::mapInterfaceToElement;

mapping uml::Package::mapPackageToElement() : eaobjectmodel::Element {
	Name := self.name;
	ElementGUID := self.resolveoneIn(uml::Package::mapPackage).PackageGUID;
	
	result.Type := ElementType::Package;
	PDATA1 := self.late resolveoneIn(uml::Package::mapPackage);
	
	SourceRelations := self.getFeature("packageImport")->oclAsType(uml::PackageImport)->mapPackageImportToConnector();
	
	
}

abstract mapping uml::Classifier::mapClassifierToElementBase(): eaobjectmodel::Element
{
	log("Mapping Classifier", self);
	ElementGUID := if ElementGUID != null then ElementGUID else generateUUID() endif;
	
	PDATA1 := null;
	
	Name := self.name;
	Abstract := self.isAbstract;
	
	Visibility := self.visibility.transformVisibility();
	
	SourceRelations += self.getFeature("generalization")->oclAsType(uml::Generalization).map mapGeneralizationToConnector();
	SourceRelations += self.getFeature("substitution")->oclAsType(uml::Substitution).map mapSubstitutionToConnector();
	
	
}

mapping uml::Class::mapClassToElement(): eaobjectmodel::Element
inherits uml::Classifier::mapClassifierToElementBase {
	log("Mapping class", self);
	
	Attributes := self.ownedAttribute-> select(p : Property | p.association.oclIsUndefined()).map mapPropertyToAttribute();
	Methods := self.ownedOperation -> map mapOperationToMethod();
	
	result.Type := eaobjectmodel::ElementType::Class;
	
	SourceRelations += self.getFeature("interfaceRealization")->oclAsType(uml::InterfaceRealization).map mapRealizationToConnector();
}

mapping uml::Interface::mapInterfaceToElement() : eaobjectmodel::Element
inherits uml::Classifier::mapClassifierToElementBase {
	log("Mapping interface", self);
	
	Attributes := self.ownedAttribute -> map mapPropertyToAttribute();
	Methods := self.ownedOperation -> map mapOperationToMethod();
	
	result.Type := eaobjectmodel::ElementType::Interface;
	
	result.Abstract := true;
}

// Comment can only be linked to one Classifier
mapping uml::Comment::mapCommentToElement() : eaobjectmodel::Element
disjuncts uml::Comment::mapCommentToElementNested, uml::Comment::mapCommentToElementNotNested;

abstract mapping uml::Comment::mapCommentToElementBase() : eaobjectmodel::Element
{
	log("Mapping comment", self);
	ElementGUID := if ElementGUID != null then ElementGUID else generateUUID() endif;
	
	PDATA1 := null;
	Notes := self.body;
	result.Type := eaobjectmodel::ElementType::Note;
}

mapping uml::Comment::mapCommentToElementNested() : eaobjectmodel::Element
inherits uml::Comment::mapCommentToElementBase
when {not self.isContainedInElement()}
{
	SourceRelations += self.getFeature("annotatedElement")->oclAsType(uml::Element)->map mapElementToNoteLink();
}

mapping uml::Comment::mapCommentToElementNotNested() : eaobjectmodel::Element
inherits uml::Comment::mapCommentToElementBase
when {self.isContainedInElement()}
{
	var annotatedElement := self.getFeature("annotatedElement")->oclAsType(uml::Element);
	var eaElement := annotatedElement.resolveoneIn(uml::Element::mapCommentableToElement);
	eaElement.SourceRelations += self.map mapElementToNoteLink();
}

mapping uml::Association::mapAssociationToConnector() : eaobjectmodel::Connector {
	log("Mapping association", self);
	ConnectorGUID := if ConnectorGUID != null then ConnectorGUID else generateUUID() endif;
		
	Name := self.name;
	
	var sourceProperty := self.memberEnd->at(1);
	var destProperty := self.memberEnd->at(2);
	// setting the source assigns this element to its parent via an eOpposite relation
	Source := sourceProperty.type.resolveone(OM::Element);
	Dest := destProperty.type.resolveone(OM::Element);
	
	// If the property was not assigned to the association it is possible to navigate to this memberEnd
	SourceIsNavigable := sourceProperty.owningAssociation = null;
	DestIsNavigable := destProperty.owningAssociation = null;
	
	Direction := result.transformDirectionType();
	
	SourceIsAggregate := self.memberEnd->at(2).transformAssociationType();
	DestIsAggregate := self.memberEnd->at(1).transformAssociationType();
	
	SourceRole := sourceProperty.name;
	DestRole := destProperty.name;
	
	if (sourceProperty.getLower() = sourceProperty.getUpper())
		then SourceCard := sourceProperty.getLower().toString()
		else SourceCard := sourceProperty.getLower().toString() + ".." + sourceProperty.getUpper().toString()
	endif;
	
	if (destProperty.getLower() = destProperty.getUpper())
		then DestCard := destProperty.getLower().toString()
		else DestCard := destProperty.getLower().toString() + ".." + destProperty.getUpper().toString()
	endif;
	
	result.Type := ConnectorType::Association;	
}

abstract mapping uml::DirectedRelationship::mapDirectedRelationShip() : OM::Connector {
	ConnectorGUID := if ConnectorGUID != null then ConnectorGUID else generateUUID() endif;
	Direction := DirectionType::SourceDestination;
	DestIsNavigable := true;
}

mapping uml::Generalization::mapGeneralizationToConnector() : eaobjectmodel::Connector
inherits uml::DirectedRelationship::mapDirectedRelationShip {
	log("Mapping generalization", self);
	
	Dest := self.general.late resolveone(OM::Element);
	result.Type := ConnectorType::Generalization;
}

mapping uml::Substitution::mapSubstitutionToConnector() : eaobjectmodel::Connector
inherits uml::DirectedRelationship::mapDirectedRelationShip {
	log("Mapping substitution", self);
	
	Dest := self.getFeature("supplier")->oclAsType(uml::NamedElement).late resolveone(OM::Element);
	result.Type := ConnectorType::Substitution;
}

mapping uml::InterfaceRealization::mapRealizationToConnector() : eaobjectmodel::Connector
inherits uml::DirectedRelationship::mapDirectedRelationShip {
	log("Mapping realization", self);
	
	Dest := self.getFeature("supplier")->oclAsType(uml::NamedElement).late resolveone(OM::Element);
	result.Type := ConnectorType::Realisation;
}

mapping uml::PackageImport::mapPackageImportToConnector() : eaobjectmodel::Connector
inherits uml::DirectedRelationship::mapDirectedRelationShip {
	log("Mapping packageImport",self);
	
	Dest := self.importedPackage.late resolveoneIn(uml::Package::mapPackageToElement);
	DestIsNavigable := true;
	result.Type := ConnectorType::Package;
	Btm_Mid_Label := "�import�";
	result.Stereotype := "import";
}

mapping uml::Element::mapElementToNoteLink() : eaobjectmodel::Connector {
	log("Mapping noteLink", self);
	
	ConnectorGUID := if ConnectorGUID != null then ConnectorGUID else generateUUID() endif;
	
	Dest := self.late resolveone(eaobjectmodel::Element);
	DestIsNavigable := true;
	Direction := DirectionType::SourceDestination;
	result.Type := ConnectorType::NoteLink;
}

mapping uml::Property::mapPropertyToAttribute(): Attribute {
	log("Mapping attribute", self);
	
	AttributeGUID := if AttributeGUID != null then AttributeGUID else generateUUID() endif;
	
	Name := self.name;

	result.Scope := self.visibility.transformVisibility();
	AttributeType := self.type.map transformType();
}

mapping uml::Operation::mapOperationToMethod() : Method {
	log("Mapping operation", self);
	
	MethodGUID := if MethodGUID != null then MethodGUID else generateUUID() endif;
	
	Name := self.name;
	Visibility := self.visibility.transformVisibility();
	
	self.ownedParameter-> forEach(param) {
		if (param.direction = ParameterDirectionKind::_return) then 
		result.ReturnType := param.type.map transformType() endif;
		
		if (param.direction <> ParameterDirectionKind::_return) then
		result.Parameters += param.map mapParameter() endif;
	};
}

mapping uml::Parameter::mapParameter() : Methodparameter {
	log("Mapping parameter", self);
	
	ea_guid := if ea_guid != null then ea_guid else generateUUID() endif;
	
	Name := self.name;
	Kind := self.direction.transformParameterDirection();
	ParameterType := self.type.map transformType();
	

}

query uml::Property::transformAssociationType() : IsAggregate {
	return switch {
		case (self.aggregation = AggregationKind::none) IsAggregate::False;
		case (self.aggregation = AggregationKind::shared) IsAggregate::Aggregation;
		case (self.aggregation = AggregationKind::composite) IsAggregate::Composition;
		else assert error (false) with log("Illegal enum value");
	};
}

query uml::ParameterDirectionKind::transformParameterDirection() : ParameterDirection {
	return switch {
		case (self = ParameterDirectionKind::_in) ParameterDirection::_in;
		case (self = ParameterDirectionKind::_out) ParameterDirection::_out;
		case (self = ParameterDirectionKind::_inout) ParameterDirection::_inout;
		case (self = ParameterDirectionKind::_return) ParameterDirection::_return;
	};
}

query VisibilityKind::transformVisibility() : Scope {
	return switch {
		case (self = VisibilityKind::public) Scope::Public;
		case (self = VisibilityKind::private) Scope::Private;
		case (self = VisibilityKind::protected) Scope::Protected;
		case (self = VisibilityKind::package) Scope::Package;
		else assert error (false) with log("Illegal enum value");
	};
}

query uml::PrimitiveType::transformPrimitive() : OM::PrimitiveType {
	return switch {
		case (self.name = "EInt") OM::PrimitiveType::Int;
		case (self.name = "EBoolean") OM::PrimitiveType::Boolean;
		case (self.name = "EByte") OM::PrimitiveType::Byte;
		case (self.name = "EChar") OM::PrimitiveType::Char;
		case (self.name = "EDouble") OM::PrimitiveType::Double;
		case (self.name = "ELong") OM::PrimitiveType::Long;
		case (self.name = "EFloat") OM::PrimitiveType::Float;
		case (self.name = "EShort") OM::PrimitiveType::Short;
		case (self.name = "EString") OM::PrimitiveType::String;
		else assert error (false) with log("Could not interpret primitive datatype \"" + self.name + "\".");
	};
}

query OM::Connector::transformDirectionType() : OM::DirectionType {
	return switch {
		case (self.SourceIsNavigable and self.DestIsNavigable) DirectionType::BiDirectional;
		case (self.SourceIsNavigable) DirectionType::DestinationSource;
		case (self.DestIsNavigable) DirectionType::SourceDestination;
		else DirectionType::Unspecified;
	};
 }

mapping uml::Type::transformType() : TypeReference {

	if (self.oclIsTypeOf(uml::PrimitiveType)) then {
		log("Transforming primitive type", self);
		result.typeName := self.oclAsType(uml::PrimitiveType).transformPrimitive().toString();
	} else {
		log("Late resolving class type", self);
		result.classifier := self.oclAsType(uml::Class).late resolveone(OM::Element);
		// FIXME: After resolving the classifier, set the typeName (Maybe not needed)
		//reference.typeName := self.oclAsType(uml::Class).late resolveone(OM::Element)->any(t | t = self.toString());
	} endif;
}

//Graphical

mapping notation::Diagram::mapDiagram() : eaobjectmodel::Diagram {
	log("Mapping diagram", self);
	
	DiagramGUID := if DiagramGUID != null then DiagramGUID else generateUUID() endif;
	
	Name := self.name;
	
	result.Type := "Logical";
	Orientation := "P";
	
	result.Package := self.element.oclAsType(uml::Package).resolveoneIn(uml::Package::mapPackage);

	var directShapes := self.children->selectByType(notation::Shape);
	directShapes->map mapShape(0, 0);	
	
	var totalShapes := self.getAllContents(false)->selectByType(notation::Shape)->select(s | s.isShapeRequired());
	log("Total Shapes:", totalShapes.element.eClass().name);
	
	var sequenceNumber := totalShapes->size();
	log("#Total Shapes:", sequenceNumber);
	
	directShapes-> forEach(shape) {
		sequenceNumber := shape.setSequence(sequenceNumber)
	};
	
	self.edges->selectByType(notation::Connector)->map mapConnector();
}

mapping notation::Shape::mapShape(packageLeft : Integer, packageTop : Integer) : eaobjectmodel::DiagramObject 
when {self.isShapeRequired()} {
	log("Mapping diagramobject", self.element.eClass().name);

	result.Element := self.element.resolveone(eaobjectmodel::Element);
	result.Diagram := self.diagram.resolveone(eaobjectmodel::Diagram);
	
	Left := self.getBounds().x + packageLeft;
	Top := packageTop - self.getBounds().y;
	Right := self.getBounds().width + result.Left;
	Bottom := result.Top - self.getBounds().height;
	
	if (self.isPackage())
		then log("NestedShapes:", self.getNestedShapes().element.eClass().name)
	endif;
	//TODO: Find best place for condition
	if (self.isPackage())
		then self.getNestedShapes()-> map mapShape(result.Left, result.Top)
	endif;
}

helper notation::Shape::setSequence(sequenceNumber : Integer) : Integer {
	var diaObject := self.resolveoneIn(notation::Shape::mapShape);
	diaObject._Sequence := sequenceNumber;
	var newSequenceNumber := sequenceNumber - 1;
	
	log("Shape and SequenceNumber", self.element.eClass().name + " : " + sequenceNumber.toString());
	
	self.getNestedShapes()-> forEach(shape) {
		newSequenceNumber := shape.setSequence(newSequenceNumber)
	};

	return newSequenceNumber;
}

mapping notation::Connector::mapConnector() : eaobjectmodel::DiagramLink 
merges notation::Connector::mapConnectorToCommentLink, notation::Connector::mapConnectorToAssociation{
	log("Mapping diagramlink", self);
		
	result.Connector := self.element.resolveone(eaobjectmodel::Connector);
	
	var sourceShape := self.source.oclAsType(notation::Shape);
	var targetShape := self.target.oclAsType(notation::Shape);
	
	result.Diagram := self.diagram.resolveone(eaobjectmodel::Diagram);
	
	var targetBounds := sourceShape.layoutConstraint.oclAsType(notation::Bounds);
	var sourceBounds := targetShape.layoutConstraint.oclAsType(notation::Bounds);
	
	result.setEX(calculateX(targetShape, self.targetAnchor));
	result.setEY(calculateY(targetShape, self.targetAnchor));
	
	result.setSX(calculateX(sourceShape, self.sourceAnchor));
	result.setSY(calculateY(sourceShape, self.sourceAnchor));
}

mapping notation::Connector::mapConnectorToCommentLink() : eaobjectmodel::DiagramLink 
when {self.type = PAPYRUS_NOTATION_EDGE_COMMENTLINK}
{
	var targetShape := self.target.oclAsType(notation::Shape);
	var targetEle := targetShape.element.oclAsType(uml::Element);
	result.Connector := targetEle.resolveoneIn(uml::Element::mapElementToNoteLink)
}

mapping notation::Connector::mapConnectorToAssociation() : eaobjectmodel::DiagramLink 
when {self.type = PAPYRUS_NOTATION_EDGE_ASSOCIATION} {
	
	var sourceShape := self.source.oclAsType(notation::Shape);
	var targetShape := self.target.oclAsType(notation::Shape);
	var targetBounds := sourceShape.layoutConstraint.oclAsType(notation::Bounds);
	var sourceBounds := targetShape.layoutConstraint.oclAsType(notation::Bounds);c
	
	result.setSX(calculateX(targetShape, self.targetAnchor));
	result.setSY(calculateY(targetShape, self.targetAnchor));
	
	result.setEX(calculateX(sourceShape, self.sourceAnchor));
	result.setEY(calculateY(sourceShape, self.sourceAnchor));
}

query notation::Shape::getNestedShapes() : OrderedSet(notation::Shape) {
	if (self.isPackage())
		then return self.children->any(c | c.oclIsTypeOf(notation::BasicCompartment)).children->selectByType(notation::Shape)
		else return new OrderedSet(notation::Shape)()
	endif;
}

query notation::Shape::isPackage() : Boolean {
	return self.type.startsWith(PAPYRUS_NOTATION_SHAPE_PACKAGE_REGULAR);
}

query notation::Shape::isShapeRequired() : Boolean {
	return self.type.startsWith(PAPYRUS_NOTATION_SHAPE_PACKAGE_REGULAR) or 
	self.type.startsWith(PAPYRUS_NOTATION_SHAPE_CLASS_REGULAR) or 
	self.type.startsWith(PAPYRUS_NOTATION_SHAPE_INTERFACE_REGULAR) or 
	self.type.startsWith(PAPYRUS_NOTATION_SHAPE_COMMENT_REGULAR)
}

query notation::Shape::getBounds() : notation::Bounds {
	return self.layoutConstraint.oclAsType(notation::Bounds);
}

query calculateX(shape : notation::Shape, anchor : notation::Anchor) : Integer {
	var bounds := shape.layoutConstraint.oclAsType(notation::Bounds);
	var width := bounds.width;
	
	var coordsString := anchor.oclAsType(notation::IdentityAnchor).id;
	var commaIndex := coordsString.indexOf(",");
	var resultString : String := coordsString.substring(2, commaIndex - 1);
	var xPos := resultString.toReal();
	
	var xReal := width * xPos - width./(2);
	return xReal.round();
}

query calculateY(shape : notation::Shape, anchor : notation::Anchor) : Integer {
	var bounds := shape.layoutConstraint.oclAsType(notation::Bounds);
	var height := bounds.height;
	
	var coordsString := anchor.oclAsType(notation::IdentityAnchor).id;
	var commaIndex := coordsString.indexOf(",");
	var resultString : String := coordsString.substring(commaIndex + 1, coordsString.length() - 1);
	var yPos := resultString.toReal();
	
	var yReal := height/(2) - yPos * height;
	return yReal.round();
}

query uml::Comment::isContainedInElement() : Boolean {
	var commentEdge := notation.rootObjects()[notation::Diagram].edges->any(
		type = PAPYRUS_NOTATION_EDGE_COMMENTLINK and
			(target.element = self.toEObject() or source.element = self.toEObject())
	);
	return commentEdge.target.element = self.toEObject();
}
