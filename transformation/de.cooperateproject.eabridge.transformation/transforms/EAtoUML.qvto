import TransformationUtils;
import de.cooperateproject.qvtoutils.CooperateLibrary;

transformation EAtoUML(in om : OM, in ecorePrimitives : UML, out uml : UML, out notation : NOTATION);

modeltype OM uses eaobjectmodel('de.cooperateproject.eabridge.eaobjectmodel');
modeltype UML uses uml('http://www.eclipse.org/uml2/5.0.0/UML');
modeltype NOTATION uses notation('http://www.eclipse.org/gmf/runtime/1.0.2/notation');
modeltype STYLE uses style('http://www.eclipse.org/papyrus/infra/viewpoints/policy/style');

// Check if its possible to adjust this that one Model only is forced
main() {
	var rootRootPackage := om.objects()[Package]->any(p: Package | p.Parent.oclIsUndefined());
	var rootPackage := rootRootPackage.Packages->any(true);
	
	rootPackage.map mapRootPackage();
	
	var diagrams := rootPackage.getAllContents()->selectByType(Diagram);
	diagrams->map diagramToDiagram();
}

// mappings for transformation to uml

// Fits to the way papyrus creates new diagrams/models
mapping eaobjectmodel::Package::mapRootPackage() : uml::Model {
	log("mapping Model", self.Name);
	
	name := self.Name;
	result.setToFeature("nestedPackage", self.Packages-> map mapPackage());
	
	// Does this reach everything?
	var connectors := self.getAllContents()->selectByType(Connector);
	
	var associations := connectors->select(e : Connector | e.Type = ConnectorType::Association or e.Type = ConnectorType::Aggregation);
	var comments := self.getAllContents()->selectByType(Element)->select(c : Element | c.Type = ElementType::Note);
	var noteLinks := connectors->select(e : Connector | e.Type = ConnectorType::NoteLink);
	var imports := connectors->select(e : Connector | e.Type = ConnectorType::Package and e.Stereotype = "import");
	
	packagedElement += associations -> map mapConnectorToAssociation();
	ownedComment += comments -> mapElementToComment();
	packageImport += imports -> mapConnectorToPackageImport();
}

mapping eaobjectmodel::Package::mapPackage() : uml::Package {
	log("mapping Package", self.Name);

	name := self.Name;
	nestedPackage := self.Packages->map mapPackage();

	var classes := self.Elements->select(e:  OM::Element | e.Type=OM::ElementType::Class);
	var interfaces := self.Elements->select(e:  OM::Element | e.Type=OM::ElementType::Interface);
	var connectors := classes->collect(SourceRelations);
	var usages := connectors->select(e : Connector| e.Type = ConnectorType::Usage);
	var Informationflows :=  connectors->select(e : Connector| e.Type = ConnectorType::InformationFlow);
	var dependencies :=  connectors->select(e : Connector| e.Type = ConnectorType::Dependency);
	
	if (usages -> size() != 0) {
		packagedElement += usages -> map mapClassesToUsage();
	};
	if (Informationflows -> size() != 0) {
		packagedElement += Informationflows -> map mapClassesToInformationflows();
	};
	if (dependencies -> size() != 0) {
		packagedElement += dependencies -> map mapClassesToDependencies();
	};
	
	packagedElement += classes-> map mapElementToClass();	
	packagedElement += interfaces-> map mapElementToInterface();
}

mapping Connector::mapClassesToUsage():  Usage {
	name := self.Name;
	supplier += self.Source.oclAsType(Class);
	supplier += self.Dest.oclAsType(Class);
}
mapping Connector::mapClassesToDependencies():  Dependency {
	name := self.Name;
	supplier += self.Source.oclAsType(Class);
	supplier += self.Dest.oclAsType(Class);	
}
mapping Connector::mapClassesToInformationflows():  InformationFlow {
	name := self.Name;
	informationSource := self.Source.oclAsType(Class);
	informationTarget := self.Dest.oclAsType(Class);
}

abstract mapping Element::mapElementToClassifier() : uml::Classifier {
	name := self.Name;
	visibility := self.Visibility.getAttributeType();
	isAbstract := if (self.Abstract.equalsIgnoreCase("1")) then true else false endif;
	
	var generalizations = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Generalization);
	var substitutions   = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Substitution);
	
	if (generalizations -> size() != 0) {
		generalization := generalizations -> map mapConnectoToGeneralization();
	};
	if (substitution -> size() != 0) {
		substitution := substitutions -> map mapConnectoToSubstitution();
	};
}

mapping Element::mapElementToClass() : uml::Class
inherits Element::mapElementToClassifier 
{	
	ownedAttribute += self.Attributes-> map transformAttribute();
	ownedOperation += self.Methods-> map transformOperations();
	
	var realizations = self.SourceRelations->select(e : Connector | e.Type = ConnectorType::Realisation);	
	var associations = self.SourceRelations->select(e : Connector | e.Type = ConnectorType::Association or e.Type = ConnectorType::Aggregation);
	
	if (realizations -> size() != 0) {
		interfaceRealization := realizations -> map mapConnectoToRealization();
	};
}

mapping Element::mapElementToInterface() :  uml::Interface
inherits Element::mapElementToClassifier
{
	ownedAttribute := self.Attributes-> map transformAttribute();
	ownedOperation := self.Methods-> map transformOperations();
	isAbstract := false;
	
	var realizations = self.SourceRelations->select(e : Connector | e.Type = ConnectorType::Realisation);	
	var associations = self.SourceRelations->select(e : Connector | e.Type = ConnectorType::Association or e.Type = ConnectorType::Aggregation);	
}

mapping Element::mapElementToComment() : uml::Comment
{
	body := self.Notes;
	
	var commentLinks = self.getAllContents(true)->selectByType(Connector)->select(e : Connector | e.Type = ConnectorType::NoteLink);
	var commentedElements = commentLinks->select(e : Connector | e.Source = self).Dest;
	commentedElements += commentLinks->select(e : Connector | e.Dest = self).Source;
	
	commentedElements->forEach(ele) {
		if (ele.PDATA1 = null)
			then annotatedElement += ele.resolveone(uml::Element)
			else annotatedElement += ele.PDATA1.resolveone(uml::Element)
		endif;
	}
}

mapping Attribute::transformAttribute() :  uml::Property
{
	name := self.Name;
	visibility := self.Scope.getAttributeType();
	if (self.AttributeType.classifier=null) 
		then type := self.AttributeType.getPrimitiveType().transformPrimitive() 
		else (type := self.AttributeType.classifier.late resolveone(uml::Classifier)) 
	endif;
	if (type=null) then (type := self.AttributeType.classifier.late resolveone(uml::Classifier)) endif;
}

mapping Connector::mapConnectoToGeneralization() :  Generalization
{
	general := self.Dest.late resolveone(uml::Classifier);
}
mapping Connector::mapConnectoToSubstitution() :  uml::Substitution
{
	supplier := self.Dest.late resolveone(uml::Classifier);
}
mapping Connector::mapConnectoToRealization() :  InterfaceRealization
{
	supplier := self.Dest.late resolveone(uml::Classifier);
	contract := self.Dest.late resolveone(uml::Interface);
}

mapping Connector::mapConnectorToPackageImport() : PackageImport {
	importedPackage := self.Dest.PDATA1.resolveone(uml::Package);
}

mapping Connector::mapConnectorToAssociation() : uml::Association
{	
	name := self.Name;
	//order of memberEnds
	memberEnd += self.map mapSourceToProperty();
	memberEnd += self.map mapDestToProperty();
	// does not work for ownedAttributes of Interfaces
	if (self.SourceIsNavigable) 
		then (self.Dest.resolveone(uml::StructuredClassifier).ownedAttribute += self.resolveoneIn(Connector::mapSourceToProperty))
		else (ownedEnd += self.resolveoneIn(Connector::mapSourceToProperty))
	endif;
	if (self.DestIsNavigable)
		then (self.Source.resolveone(uml::StructuredClassifier).ownedAttribute += self.resolveoneIn(Connector::mapDestToProperty))
		else (ownedEnd += self.resolveoneIn(Connector::mapDestToProperty))
	endif;
}

//for more properties the semikolon seperated lists SourceStyle and DestSystle need to be parsed
mapping Connector::mapSourceToProperty() : uml::Property
{
	//EA uses the aggregation property the other way round
	aggregation := self.DestIsAggregate.transformAssociationType();
	name := self.SourceRole;
	
	type := self.Source.late resolveone(uml::Classifier);
	if (self.getSourceLower() != 1) then lower := self.getSourceLower() endif;
	if (self.getSourceUpper() != 1) then upper := self.getSourceUpper() endif;
}

mapping Connector::mapDestToProperty() : uml::Property
{
	//EA uses the aggregation property the other way round
	aggregation := self.SourceIsAggregate.transformAssociationType();
	name := self.DestRole;
	
	type := self.Dest.late resolveone(uml::Classifier);
	if (self.getDestLower() != 1) then lower := self.getDestLower() endif;
	if (self.getDestUpper() != 1) then upper := self.getDestUpper() endif;
}

query IsAggregate::transformAssociationType() : uml::AggregationKind
{
	return switch {
		case (self = IsAggregate::False) AggregationKind::none;
		case (self = IsAggregate::Aggregation) AggregationKind::shared;
		case (self = IsAggregate::Composition) AggregationKind::composite;
		else assert error (false) with log("Illegal enum value");
	};
}

mapping Method::transformOperations() :  Operation
{
	name := self.Name;
	ownedParameter := self.Parameters -> map transformParameters();
	ownedParameter += self.ReturnType.map TypeReferenceToParamter();
	visibility := self.Visibility.getAttributeType();
	
}
mapping Methodparameter::transformParameters() :  uml::Parameter
{
	name := self.Name;
	if (self.ParameterType.classifier=null) 
		then type := self.ParameterType.getPrimitiveType().transformPrimitive() 
		else (type := self.ParameterType.classifier.late resolveone(uml::Classifier)) 
	endif;
	direction := ParameterDirectionKind::_in;	
	
}

mapping in TypeReference::TypeReferenceToParamter() : uml::Parameter 
{
	direction := ParameterDirectionKind::_return;
	if (self.classifier=null) 
		then type := self.getPrimitiveType().transformPrimitive() 
		else (type := self.classifier.late resolveone(uml::Classifier)) 
	endif;

}

// queries for transformation to uml

query PrimitiveType::transformPrimitive() : uml::PrimitiveType
{
	var type = switch {
		case (self = PrimitiveType::Int) getPrimitive('EInt');
		case (self = PrimitiveType::Boolean) getPrimitive('EBoolean');
		case (self = PrimitiveType::Byte) getPrimitive('EByte');
		case (self = PrimitiveType::Char) getPrimitive('EChar');
		case (self = PrimitiveType::Double) getPrimitive('EDouble');
		case (self = PrimitiveType::Float) getPrimitive('EFloat');
		case (self = PrimitiveType::Long) getPrimitive('ELong');
		case (self = PrimitiveType::Short) getPrimitive('EShort');
		case (self = PrimitiveType::String) getPrimitive('EString');
	};
	assert warning (type <> null) with log ('Could not interpret primitive datatype \"' + self.toString() + '\".');
	return type;
}

query getPrimitive(typeName : String) : uml::PrimitiveType
{
	var model := ecorePrimitives.rootObjects()[uml::Model]->any(true);
	var primitives := model.ownedElement->selectByType(uml::PrimitiveType);
	return primitives->any(typeName = name);
}

query Scope::getAttributeType() : VisibilityKind
{
 return switch {
		case (self = Scope::Private)  VisibilityKind::private; 
		case (self = Scope::Public) VisibilityKind::public;
		case (self = Scope::Package) VisibilityKind::package;
		case (self = Scope::Protected) VisibilityKind::protected;
		else assert error (false) with log("Illegal enum value");
	};
} 


query ScopeObject::getAttributeType() : VisibilityKind
{
	return if self = null then null else self.oclAsType(Scope).getAttributeType() endif;
}

//Diagram
mapping Diagram::diagramToDiagram() : notation::Diagram {
	type := "PapyrusUMLClassDiagram";
	name := self.Name;
	measurementUnit := notation::MeasurementUnit::Pixel;
	
	var package := self.Package.resolveone(uml::Package);
	element := package.toEObject();
	
	var directChildren = self.DiagramObjects->select(d: DiagramObject | d.Element.Package = self.Package);
	
	children += directChildren-> map diagramObjectToShape();
	
	edges += self.DiagramLinks-> map diagramLinkToConnector();
	
	styles += self.map stringValueStyle();
	styles += self.map diagramStyle();
	styles += self.map papyrusViewStyle(package);
}

//Shapes
mapping DiagramObject::diagramObjectToShape() : Shape
merges DiagramObject::packageToShape, DiagramObject::classToShape, DiagramObject::interfaceToShape, DiagramObject::commentToShape {
	element := self.Element.resolveone(uml::Element).toEObject();
	
	layoutConstraint := self.map diagramObjectToLayoutConstraint();
}

mapping DiagramObject::packageToShape() : Shape 
when {self.Element.Type = ElementType::Package} {
	element := self.Element.PDATA1.resolveone(uml::Package).toEObject();
	type := self.getNestedName(PAPYRUS_NOTATION_SHAPE_PACKAGE_REGULAR);
	children += self.map diagramObjectToNameDecorationNode();
	
	children += self.map packageToCompartment();
}

mapping DiagramObject::classToShape() : Shape 
when {self.Element.Type = ElementType::Class} {
	type := self.getNestedName(PAPYRUS_NOTATION_SHAPE_CLASS_REGULAR);
	children += self.map diagramObjectToNameDecorationNode();
	
	children += self.map diagramObjectToAttributesCompartment();
	children += self.map diagramObjectToOperationsCompartment();
	children += self.map diagramObjectToSignalsCompartment();
	
	children += self.map diagramObjectToFloatingLabelDecorationNode();
}

mapping DiagramObject::interfaceToShape() : Shape
when {self.Element.Type = ElementType::Interface} {
	type := self.getNestedName(PAPYRUS_NOTATION_SHAPE_INTERFACE_REGULAR);
	children += self.map diagramObjectToNameDecorationNode();
	
	children += self.map diagramObjectToAttributesCompartment();
	children += self.map diagramObjectToOperationsCompartment();
	children += self.map diagramObjectToSignalsCompartment();
	
	children += self.map diagramObjectToFloatingLabelDecorationNode();
}

mapping DiagramObject::commentToShape() : Shape
when {self.Element.Type = ElementType::Note} {
	type := self.getNestedName(PAPYRUS_NOTATION_SHAPE_COMMENT_REGULAR);
	
	children += self.map diagramObjectToLabelBodyHintDecoration();
}


//CommentDecorationNode
mapping DiagramObject::diagramObjectToLabelBodyHintDecoration() : notation::DecorationNode
{
	type := self.getNestedName(PAPYRUS_NOTATION_NODE_COMMENT_BODY_REGULAR);
}

//NameDecorationNode
mapping DiagramObject::diagramObjectToNameDecorationNode() : DecorationNode
merges DiagramObject::packageToNameDecorationNode, DiagramObject::classToNameDecorationNode, DiagramObject::interfaceToNameDecorationNode {
}

mapping DiagramObject::packageToNameDecorationNode() : DecorationNode 
when {self.Element.Type = ElementType::Package} {
	type := self.getNestedName(PAPYRUS_NOTATION_NODE_PACKAGE_NAME_REGULAR);
}

mapping DiagramObject::classToNameDecorationNode() : DecorationNode 
when {self.Element.Type = ElementType::Class} {
	type := self.getNestedName(PAPYRUS_NOTATION_NODE_CLASS_NAME_REGULAR);
}

mapping DiagramObject::interfaceToNameDecorationNode() : DecorationNode 
when {self.Element.Type = ElementType::Interface} {
	type := self.getNestedName(PAPYRUS_NOTATION_NODE_INTERFACE_NAME_REGULAR);
}

//LayoutConstraint
mapping DiagramObject::diagramObjectToLayoutConstraint() : Bounds {
	width := self.Right - self.Left;
	height := (self.Bottom - self.Top) * -1;
	x := self.Left - self.getPackageLeft();
	y := (self.Top - self.getPackageTop()) * -1;
}

//FloatingLabelDecorationNode
mapping DiagramObject::diagramObjectToFloatingLabelDecorationNode() : DecorationNode
merges DiagramObject::classToFloatingLabelDecorationNode, DiagramObject::interfaceToFloatingLabelDecorationNode {
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping DiagramObject::classToFloatingLabelDecorationNode() : DecorationNode 
when {self.Element.Type = ElementType::Class} {
	type := self.getNestedName(PAPYRUS_NOTATION_NODE_CLASS_FLOATINGLABEL_REGULAR);
}

mapping DiagramObject::interfaceToFloatingLabelDecorationNode() : DecorationNode 
when {self.Element.Type = ElementType::Interface} {
	type := self.getNestedName(PAPYRUS_NOTATION_NODE_INTERFACE_FLOATINGLABEL_REGULAR);
}

//PackageCompartment
mapping DiagramObject::packageToCompartment() : notation::BasicCompartment
{
	type := self.getNestedName(PAPYRUS_NOTATION_COMPARTMENT_PACKAGE_PACKAGEABLE_REGULAR);
	styles += result.toEObject().map titleStyle();
	layoutConstraint := result.toEObject().map notationBounds();
	
	var directChildren = self.Diagram.DiagramObjects->select(d: DiagramObject | d.Element.Package = self.Element.PDATA1);
	children += directChildren->map diagramObjectToShape();
}

//AttributeCompartment
mapping DiagramObject::diagramObjectToAttributesCompartment() : BasicCompartment
merges DiagramObject::classToAttributesCompartment, DiagramObject::interfaceToAttributesCompartment {
	children += self.Element.Attributes-> map attributeToShape();
	styles += result.toEObject().map titleStyle();
	styles += result.toEObject().map sortingStyle();
	styles += result.toEObject().map filteringStyle();
	layoutConstraint := result.toEObject().map notationBounds();
}

mapping DiagramObject::classToAttributesCompartment() : BasicCompartment
when {self.Element.Type = ElementType::Class} {
	type := self.getNestedName(PAPYRUS_NOTATION_COMPARTMENT_CLASS_PROPERTIES_REGULAR);
}

mapping DiagramObject::interfaceToAttributesCompartment() : BasicCompartment
when {self.Element.Type = ElementType::Interface} {
	type := self.getNestedName(PAPYRUS_NOTATION_COMPARTMENT_INTERFACE_PROPERTIES_REGULAR);
}

//Attribute
mapping Attribute::attributeToShape() : Shape
merges Attribute::classAttributeToShape, Attribute::interfaceAttributeToShape {
	element := self.resolveone(uml::Property).toEObject();
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping Attribute::classAttributeToShape() : Shape
when {self.Parent.Type = ElementType::Class} {	
	type := PAPYRUS_NOTATION_SHAPE_CLASS_ATTRIBUTE;
}

mapping Attribute::interfaceAttributeToShape() : Shape
when {self.Parent.Type = ElementType::Interface} {	
	type := PAPYRUS_NOTATION_SHAPE_INTERFACE_ATTRIBUTE;
}

//MethodCompartment
mapping DiagramObject::diagramObjectToOperationsCompartment() : BasicCompartment
merges DiagramObject::classToOperationsCompartment, DiagramObject::interfaceToOperationsCompartment {
	children += self.Element.Methods-> methodToShape();
	styles += result.toEObject().map titleStyle();
	styles += result.toEObject().map sortingStyle();
	styles += result.toEObject().map filteringStyle();
	layoutConstraint := result.toEObject().map notationBounds();
}

mapping DiagramObject::classToOperationsCompartment() : BasicCompartment
when {self.Element.Type = ElementType::Class} {
	type := self.getNestedName(PAPYRUS_NOTATION_COMPARTMENT_CLASS_OPERATIONS_REGULAR);
}

mapping DiagramObject::interfaceToOperationsCompartment() : BasicCompartment
when {self.Element.Type = ElementType::Interface} {
	type := self.getNestedName(PAPYRUS_NOTATION_COMPARTMENT_INTERFACE_OPERATIONS_REGULAR);
}

//Method !Does not need nested postfix!
mapping Method::methodToShape() : Shape
merges Method::classMethodToShape, Method::interfaceMethodToShape {
	element := self.resolveone(uml::Operation).toEObject();
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping Method::classMethodToShape() : Shape
when {self.Parent.Type = ElementType::Class} {	
	type := PAPYRUS_NOTATION_SHAPE_CLASS_OPERATION
}

mapping Method::interfaceMethodToShape() : Shape
when {self.Parent.Type = ElementType::Interface} {	
	type := PAPYRUS_NOTATION_SHAPE_INTERFACE_OPERATION;
}

//SignalCompartment
mapping DiagramObject::diagramObjectToSignalsCompartment() : notation::BasicCompartment
merges DiagramObject::classToSignalsCompartment, DiagramObject::interfaceToSignalsCompartment
{
	styles += result.toEObject().map titleStyle();
	styles += result.toEObject().map sortingStyle();
	styles += result.toEObject().map filteringStyle();
	layoutConstraint := result.toEObject().map notationBounds();
}

mapping DiagramObject::classToSignalsCompartment() : notation::BasicCompartment
when {self.Element.Type = ElementType::Class} {
	type := self.getNestedName(PAPYRUS_NOTATION_COMPARTMENT_CLASS_NESTED_REGULAR);
}

mapping DiagramObject::interfaceToSignalsCompartment() : notation::BasicCompartment
when {self.Element.Type = ElementType::Interface} {
	type := self.getNestedName(PAPYRUS_NOTATION_COMPARTMENT_INTERFACE_NESTED_REGULAR);
}

//Connectors
mapping DiagramLink::diagramLinkToConnector() : notation::Connector
merges DiagramLink::associationToConnector, DiagramLink::generalizationToConnector, DiagramLink::implementationToConnector, DiagramLink::noteLinkToConnector, DiagramLink::packageImportToConnector
{
	source := self.Connector.Source.resolveElement(self.Diagram);
	target := self.Connector.Dest.resolveElement(self.Diagram);
	targetAnchor := self.map diagramLinkToTargetAnchor();
	sourceAnchor := self.map diagramLinkToSourceAnchor();

	bendpoints := result.map connectorToBendpoint();
	styles += result.toEObject().map fontStyle();
}

mapping DiagramLink::associationToConnector() : notation::Connector
when {self.Connector.Type = ConnectorType::Association or self.Connector.Type = ConnectorType::Aggregation} {
	type := PAPYRUS_NOTATION_EDGE_ASSOCIATION;
	element := self.Connector.resolveoneIn(Connector::mapConnectorToAssociation).toEObject();
	
	//switcheroo needed for some reason
	//checked with the test model
	source := self.Connector.Dest.resolveElement(self.Diagram);
	target := self.Connector.Source.resolveElement(self.Diagram);
	
	children += self.map diagramLinkToNameDecoration();
	children += self.map diagramLinkToTargetRoleDecoration();
	children += self.map diagramLinkToSourceRoleDecoration();
	children += self.map diagramLinkToTargetMultiplicityDecoration();
	children += self.map diagramLinkToSourceMultiplicityDecoration();
}

mapping DiagramLink::generalizationToConnector() : notation::Connector
when {self.Connector.Type = ConnectorType::Generalization} {
	type := PAPYRUS_NOTATION_EDGE_GENERALIZATION;
	element := self.Connector.resolveoneIn(Connector::mapConnectoToGeneralization).toEObject();
}

mapping DiagramLink::implementationToConnector() : notation::Connector
when {self.Connector.Type = ConnectorType::Realisation} {
	type := PAPYRUS_NOTATION_EDGE_IMPLEMENTATION;
	element := self.Connector.resolveoneIn(Connector::mapConnectoToRealization).toEObject();
}

mapping DiagramLink::noteLinkToConnector() : notation::Connector
when {self.Connector.Type = ConnectorType::NoteLink} {
	type := PAPYRUS_NOTATION_EDGE_COMMENTLINK;
}

mapping DiagramLink::packageImportToConnector() : notation::Connector 
when {self.Connector.Type = ConnectorType::Package and self.Connector.Stereotype = "import"} {
	type := PAPYRUS_NOTATION_EDGE_PACKAGEIMPORT;
	element := self.Connector.resolveoneIn(Connector::mapConnectorToPackageImport).toEObject();
}

//ConnectorNameDecorationNode
mapping DiagramLink::diagramLinkToNameDecoration() : DecorationNode
{
	type := PAPYRUS_NOTATION_NODE_ASSOCIATION_NAME;
	layoutConstraint := result.toEObject().map locationConstraint();
	styles += result.toEObject().map booleanValueStyle();
}

//RoleDecorationNode
mapping DiagramLink::diagramLinkToTargetRoleDecoration() : DecorationNode
{
	type := PAPYRUS_NOTATION_NODE_ASSOCIATION_TARGETROLE;
	layoutConstraint := result.toEObject().map locationConstraint();
	styles += result.toEObject().map booleanValueStyle();
}

mapping DiagramLink::diagramLinkToSourceRoleDecoration() : DecorationNode
{
	type := PAPYRUS_NOTATION_NODE_ASSOCIATION_SOURCEROLE;
	layoutConstraint := result.toEObject().map locationConstraint();
	styles += result.toEObject().map booleanValueStyle();
}

//MultiplicityDecorationNode
mapping DiagramLink::diagramLinkToTargetMultiplicityDecoration() : notation::DecorationNode
{
	type := PAPYRUS_NOTATION_NODE_ASSOCIATION_TARGETMULTIPLICITY;
	layoutConstraint := result.toEObject().map locationConstraint();
	styles += result.toEObject().map booleanValueStyle();
}

mapping DiagramLink::diagramLinkToSourceMultiplicityDecoration() : notation::DecorationNode
{
	type := PAPYRUS_NOTATION_NODE_ASSOCIATION_SOURCEMULTIPLICITY;
	layoutConstraint := result.toEObject().map locationConstraint();
	styles += result.toEObject().map booleanValueStyle();
}

//Anchors
mapping DiagramLink::diagramLinkToTargetAnchor() : notation::IdentityAnchor 
{
	var bounds := self.Connector.Dest.resolveElement(self.Diagram).layoutConstraint.oclAsType(notation::Bounds);
	var xPos := (self.SX + bounds.width./(2)) / bounds.width;
	var yPos := (self.SY.-() + bounds.height./(2)) / bounds.height;
	id := "(" + xPos.toString() + "," + yPos.toString() + ")";
}

mapping DiagramLink::diagramLinkToSourceAnchor() : notation::IdentityAnchor 
{
	var bounds := self.Connector.Source.resolveElement(self.Diagram).layoutConstraint.oclAsType(notation::Bounds);
	var xPos := (self.EX + bounds.width./(2)) / bounds.width;
	var yPos := (self.EY.-() + bounds.height./(2)) / bounds.height;
	id := "(" + xPos.toString() + "," + yPos.toString() + ")";
}

//Queries
query DiagramObject::getNestedName(name : String) : String {
	if (self.Diagram.Package = self.Element.Package)
		then return name
		else return name + "_CN"
	endif	
}

// om.objectsOfType allowed here because it's used for finding a specific entity'
query DiagramObject::getPackageLeft() : Integer {
	var package := om.objectsOfType(DiagramObject)->any(p: DiagramObject | p.Diagram = self.Diagram and p.Element.PDATA1 = self.Element.Package);
	if (package.oclIsUndefined())
		then return 0
		else return package.Left
	endif;
}

query DiagramObject::getPackageTop() : Integer {
	var package := om.objectsOfType(DiagramObject)->any(p: DiagramObject | p.Diagram = self.Diagram and p.Element.PDATA1 = self.Element.Package);
	if (package.oclIsUndefined())
		then return 0
		else return package.Top
	endif;
}

query Element::resolveElement(dia : eaobjectmodel::Diagram) : notation::Shape {
	var diaObj = om.objectsOfType(DiagramObject)->any(diaObj: DiagramObject| diaObj.Diagram = dia and diaObj.Element = self);
	assert fatal (not diaObj.oclIsUndefined());
	return diaObj.resolveoneIn(DiagramObject::diagramObjectToShape);
}

//Style stuff
mapping eaobjectmodel::Diagram::stringValueStyle() : notation::StringValueStyle
{
	name := "diagram_compatibility_version";
	stringValue := "1.2.0";	
}

mapping eaobjectmodel::Diagram::diagramStyle() : notation::DiagramStyle
{	
}

mapping eaobjectmodel::Diagram::papyrusViewStyle(rootElement : uml::Package) : style::PapyrusViewStyle
{
	owner := rootElement.toEObject();
}
