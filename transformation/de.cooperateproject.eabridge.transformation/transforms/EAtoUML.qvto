transformation EAtoUML(inout om: OM,in ecorePrimitives : UML, out uml1:UML);
modeltype OM uses eaobjectmodel('de.cooperateproject.eabridge.eaobjectmodel');
modeltype UML uses uml('http://www.eclipse.org/uml2/5.0.0/UML');

main() {
	var rootPackages := om.objects()[Package]->select(p: Package | p.Parent.oclIsUndefined());
	rootPackages-> map mapRootPackages();
}

// Fits to the way papyrus creates new diagrams/models
mapping eaobjectmodel::Package::mapRootPackages() : uml::Model {
	
	name := self.Name;
	nestedPackage := self.Packages-> map mapPackages();
}

mapping eaobjectmodel::Package::mapPackages() : uml::Package
{
	name := self.Name;
	nestedPackage := self.Packages->map mapPackages();

	var classes := self.Elements->select(e:  OM::Element | e.Type=OM::ElementType::Class);
	var connectors := classes->collect(SourceRelations);
	var usages := connectors->select(e : Connector| e.Type = ConnectorType::Usage);
	var Informationflows :=  connectors->select(e : Connector| e.Type = ConnectorType::InformationFlow);
	var dependencies :=  connectors->select(e : Connector| e.Type = ConnectorType::Dependency);
	if (usages -> size() != 0) {
		packagedElement += usages -> map mapClassesToUsage();
	};
	if (Informationflows -> size() != 0) {
		packagedElement += Informationflows -> map mapClassesToInformationflows();
	};
	if (dependencies -> size() != 0) {
		packagedElement += dependencies -> map mapClassesToDependencies();
	};
	packagedElement += classes-> map mapElementsToClasses();
	
	//packagedElement += self.Elements->select(e:  OM::Element | e.Type="Interface")-> map mapElementsToClasses();
}

mapping Connector::mapClassesToUsage():  Usage {
	name := self.Name;
	supplier += self.Source.oclAsType(Class);
	supplier += self.Dest.oclAsType(Class);
}
mapping Connector::mapClassesToDependencies():  Dependency {
	name := self.Name;
	supplier += self.Source.oclAsType(Class);
	supplier += self.Dest.oclAsType(Class);	
}
mapping Connector::mapClassesToInformationflows():  InformationFlow {
	name := self.Name;
	informationSource := self.Source.oclAsType(Class);
	informationTarget := self.Dest.oclAsType(Class);

}
mapping Element::mapElementsToClasses() : Class
{	
	name := self.Name;
	ownedAttribute := self.Attributes-> map transformAttribute();
	ownedOperation := self.Methods-> map transformOperations();
	visibility := self.Visibility.getAttributeType();
	
	var associations = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Association);
	var aggregations = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Aggregation);
	var abstractions = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Abstraction);
	var generalizations = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Generalization);
	var extensions = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Extension);
	var realizations = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Realisation);
	var substitutions   = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Substitution);
	var usages   = self.SourceRelations->select(e : Connector| e.Type = ConnectorType::Usage);
	
	if (associations -> size() != 0) {
		nestedClassifier := associations -> map mapElementsToConnectors();
	};
	
	if (aggregations -> size() != 0) {
		nestedClassifier := aggregations -> map mapElementsToConnectors();
	};
	
	if (generalizations -> size() != 0) {
		generalization := generalizations -> map mapConnectoToGeneralization();
	};
	if (realizations -> size() != 0) {
		interfaceRealization := realizations -> map mapConnectoToRealization();
	};
	if (substitution -> size() != 0) {
		substitution := substitutions -> map mapConnectoToSubstitution();
	};
	if (substitution -> size() != 0) {
		substitution := substitutions -> map mapConnectoToSubstitution();
	};
	
}
mapping Element::mapElementsToInterfaces() :  uml::Interface
{
	name := self.Name;
}

mapping Attribute::transformAttribute() :  uml::Property
{
	name := self.Name;
	visibility := self.Scope.getAttributeType();
	var test := self.AttributeType.getPrimitiveType();
	log (test.toString());
	type := self.AttributeType.typeName.transformPrimitive();
	if (type=null) then (type := self.AttributeType.classifier.late resolveone(uml::Classifier)) endif;
}

mapping Connector::mapConnectoToGeneralization() :  Generalization
{
	general := self.Dest.oclAsType(Class);
}
mapping eaobjectmodel::Connector::mapConnectoToSubstitution() :  uml::Substitution
{
	supplier := self.Dest.oclAsType(Class);
}
mapping Connector::mapConnectoToRealization() :  InterfaceRealization
{
	
}
mapping Connector::mapElementsToConnectors() : uml::Association
{	
	if (self.Type = ConnectorType::Aggregation) {
		ownedEnd += self -> map transformConnectorToAggregation();	
	} else {
		ownedEnd += self -> map transformConnectorToAssociation();
	};	
}

mapping Connector::transformConnectorToAggregation() :  Property
{
	name := self.Source.Name;
	aggregation := AggregationKind::shared;
	type :=	self.Source.map mapElementsToClasses().oclAsType(Type)
}

mapping Connector::transformConnectorToAssociation() :  Property
{
	name := self.Source.Name;
	type :=	self.Source.map mapElementsToClasses().oclAsType(Type);
	upperValue := self.SourceCard.map StringToCardinality().oclAsType(ValueSpecification);
	lowerValue := self.DestCard.map StringToCardinality().oclAsType(ValueSpecification);
}


mapping String::StringToCardinality() :  LiteralInteger
{
	value := self.toInteger();
}


mapping Element::transformConnectorToComposition() :  Property
{
	name := self.Name;
	aggregation := AggregationKind::composite;
	type :=	self.map mapElementsToClasses().oclAsType(Type)
}

mapping Method::transformOperations() :  Operation
{
	name := self.Name;
	ownedParameter := self.Parameters -> map transformParameters();
	ownedParameter += self.ReturnType.map TypeReferenceToParamter();
	visibility := self.Visibility.getAttributeType();
	
}
mapping Methodparameter::transformParameters() :  uml::Parameter
{
	name := self.Name;
	type := self.ParameterType.typeName.transformPrimitive();
	if (type=null) then (type := self.ParameterType.classifier.late resolveone(uml::Classifier)) endif;
	direction := ParameterDirectionKind::_in;	
	
}

mapping in TypeReference::TypeReferenceToParamter() : uml::Parameter 
{
	direction := ParameterDirectionKind::_return;
	type := self.typeName.transformPrimitive();
	if (type=null) then (type := self.classifier.late resolveone(uml::Classifier)) endif;

}

query String::transformPrimitive() : uml::PrimitiveType
{
	var searchString = "E".concat(self).toLower();
	var model := ecorePrimitives.rootObjects()[uml::Model]->any(true);
	var types := model.ownedElement->selectByType(uml::PrimitiveType);
	var type := types->any(searchString = name.toString().toLower());
	assert warning (type <> null) with log ('Could not interpret primitive datatype \"' + self + '\"');
	return type;
}

query Scope::getAttributeType() : VisibilityKind
{
 return switch {
		case (self = Scope::Private)  VisibilityKind::private; 
		case (self = Scope::Public) VisibilityKind::public;
		case (self = Scope::Package) VisibilityKind::package;
		case (self = Scope::Protected) VisibilityKind::protected;
		else assert error (false) with log("Illegal enum value");
	};
} 


query ScopeObject::getAttributeType() : VisibilityKind
{
	return if self = null then null else self.oclAsType(Scope).getAttributeType() endif;
}
